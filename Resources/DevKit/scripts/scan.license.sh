#!/bin/zsh

cd "$(dirname "$0")"

while [[ ! -d .git ]] && [[ "$(pwd)" != "/" ]]; do
    cd ..
done

if [[ -d .git ]] && [[ -d MuseAmp.xcworkspace ]]; then
    echo "[*] found project root: $(pwd)"
else
    echo "[!] could not find project root"
    exit 1
fi

PROJECT_ROOT=$(pwd)
PACKAGE_CLONE_ROOT="${PROJECT_ROOT}/.build/license.scanner/dependencies"

function with_retry {
    local retries=3
    local count=0
    while [[ $count -lt $retries ]]; do
        "$@"
        if [[ $? -eq 0 ]]; then
            return 0
        fi
        count=$((count + 1))
    done
    return 1
}

if [[ -n $(git status --porcelain) ]]; then
    if [[ "${ALLOW_DIRTY:-0}" == "1" ]]; then
        echo "[*] git is not clean; continuing because ALLOW_DIRTY=1"
    else
        echo "[!] git is not clean"
        exit 1
    fi
fi

echo "[*] resolving packages..."

RESOLVE_SCHEMES=("MuseAmp" "MuseAmpTV")

for scheme in "${RESOLVE_SCHEMES[@]}"; do
    echo "[*] resolving scheme: $scheme"
    with_retry xcodebuild -resolvePackageDependencies \
        -clonedSourcePackagesDirPath "$PACKAGE_CLONE_ROOT" \
        -workspace *.xcworkspace \
        -scheme "$scheme" |
        xcbeautify --disable-colored-output --disable-logging
done

echo "[*] scanning licenses..."

SCANNER_DIR=(
    "$PROJECT_ROOT/Resources/AdditionalLicenses"
    "$PACKAGE_CLONE_ROOT/checkouts"
    "$PROJECT_ROOT/Vendor"
)

# Build package name mapping from Package.resolved
declare -A PACKAGE_NAME_MAP
PACKAGE_RESOLVED="${PROJECT_ROOT}/MuseAmp.xcworkspace/xcshareddata/swiftpm/Package.resolved"

# Manual corrections for packages where GitHub URL does not match desired display name
declare -A MANUAL_CORRECTIONS=(
    ["listviewkit"]="ListViewKit"
    ["wcdb-spm-prebuilt"]="WCDB"
)

if [[ -f "$PACKAGE_RESOLVED" ]]; then
    echo "[*] reading package names from Package.resolved..."
    while IFS= read -r line; do
        if [[ $line =~ \"identity\"[[:space:]]*:[[:space:]]*\"([^\"]+)\" ]]; then
            identity="${match[1]}"
            if [[ -n "${MANUAL_CORRECTIONS[$identity]}" ]]; then
                PACKAGE_NAME_MAP[$identity]="${MANUAL_CORRECTIONS[$identity]}"
            else
                read -r location_line
                if [[ $location_line =~ \"location\"[[:space:]]*:[[:space:]]*\"[^/]+/([^/\"]+)\" ]]; then
                    repo_name="${match[1]}"
                    repo_name="${repo_name%.git}"
                    PACKAGE_NAME_MAP[$identity]="$repo_name"
                fi
            fi
        fi
    done < <(grep -A 1 '"identity"' "$PACKAGE_RESOLVED")
fi

function get_correct_package_name {
    local dir_name=$1
    local lowercase_name=$(echo "$dir_name" | tr '[:upper:]' '[:lower:]')

    if [[ -n "${PACKAGE_NAME_MAP[$lowercase_name]}" ]]; then
        echo "${PACKAGE_NAME_MAP[$lowercase_name]}"
    else
        echo "$dir_name"
    fi
}

SCANNED_LICENSE_CONTENT="# Open Source License\n\n"

for dir in "${SCANNER_DIR[@]}"; do
    if [[ -d "$dir" ]]; then
        for file in $(find "$dir" -maxdepth 2 -name "LICENSE*" -type f); do
            PACKAGE_NAME=$(get_correct_package_name $(basename $(dirname $file)))

            # skip debug-only tools with incompatible licenses
            if [[ "$PACKAGE_NAME" == "LookInside" ]]; then
                continue
            fi

            SCANNED_LICENSE_CONTENT="${SCANNED_LICENSE_CONTENT}\n\n## ${PACKAGE_NAME}\n\n$(cat $file)"
        done
        for file in $(find "$dir" -maxdepth 2 -name "COPYING*" -type f); do
            PACKAGE_NAME=$(get_correct_package_name $(basename $(dirname $file)))

            # skip debug-only tools with incompatible licenses
            if [[ "$PACKAGE_NAME" == "LookInside" ]]; then
                continue
            fi

            SCANNED_LICENSE_CONTENT="${SCANNED_LICENSE_CONTENT}\n\n## ${PACKAGE_NAME}\n\n$(cat $file)"
        done
    fi
done

LICENSE_OUTPUTS=(
    "$PROJECT_ROOT/MuseAmp/Resources/OpenSourceLicenses.md"
    "$PROJECT_ROOT/MuseAmpTV/Resources/OpenSourceLicenses.md"
)

for output_path in "${LICENSE_OUTPUTS[@]}"; do
    mkdir -p "$(dirname "$output_path")"
    echo -e "$SCANNED_LICENSE_CONTENT" >"$output_path"
    echo "[*] wrote $output_path"
done

echo "[*] checking for incompatible licenses..."

INCOMPATIBLE_LICENSES_KEYWORDS=(
    "GNU General Public License"
    "GNU Lesser General Public License"
    "GNU Affero General Public License"
)

for output_path in "${LICENSE_OUTPUTS[@]}"; do
    for keyword in "${INCOMPATIBLE_LICENSES_KEYWORDS[@]}"; do
        if grep -q "$keyword" "$output_path"; then
            echo "[!] found incompatible license: $keyword in $output_path"
            exit 1
        fi
    done
done

echo "[*] formatting license files with prettier..."
npx --yes prettier --write "${LICENSE_OUTPUTS[@]}"

echo "[*] done"
